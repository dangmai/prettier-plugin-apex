buildscript {
  dependencies {
    // https://mvnrepository.com/artifact/cz.habarta.typescript-generator/typescript-generator-gradle-plugin
    classpath("cz.habarta.typescript-generator:typescript-generator-gradle-plugin:3.2.1263")

    // https://mvnrepository.com/artifact/org.apache.groovy/groovy-json
    classpath("org.apache.groovy:groovy-json:5.0.4")
  }
}

plugins {
  id 'java'
  id 'application'
  id 'jacoco'
}
apply plugin: 'cz.habarta.typescript-generator'

sourceSets {
  test{
    compileClasspath += project(':parser').sourceSets.testFixtures.compileClasspath
    runtimeClasspath += project(':parser').sourceSets.testFixtures.runtimeClasspath
    resources {
      srcDirs += [project(':parser').sourceSets.test.resources]
    }
  }
}

task parserStartScripts(type: CreateStartScripts) {
  mainClass.set('net.dangmai.serializer.Apex')
  applicationName = 'apex-ast-serializer'
  defaultJvmOpts = [
    "--add-opens=java.base/java.lang=ALL-UNNAMED",
    "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
    "--add-opens=java.base/java.util=ALL-UNNAMED",
    "--add-opens=java.base/java.text=ALL-UNNAMED"
  ]
  classpath = startScripts.classpath
  outputDir = startScripts.outputDir
}

application {
  mainClass.set("net.dangmai.serializer.server.HttpServer")
  applicationName = "apex-ast-serializer-http"
  applicationDefaultJvmArgs = [
    "--add-opens=java.base/java.lang=ALL-UNNAMED",
    "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
    "--add-opens=java.base/java.util=ALL-UNNAMED",
    "--add-opens=java.base/java.text=ALL-UNNAMED"
  ]
  applicationDistribution.into("bin") {
    from(parserStartScripts)
    filePermissions {
      user {
        read = true
        write = true
        execute = true
      }
      group {
        read = true
        write = true
      }
      other {
        read = true
        write = true
      }
    }
  }

  applicationDistribution.from("build/typings") {
    into "typings"
  }
}

java {
  group = 'net.dangmai'
  version = new groovy.json.JsonSlurper().parse(file('../../../package.json')).version

  // We can't use Gradle's "release" feature because of this bug with GraalVM native build tools:
  // https://github.com/oracle/graal/issues/7414
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

processResources {
  filesMatching('**/server.properties') {
    filter {
      it.replace('0.0.0-DEV', version)
    }
  }
}

repositories {
  flatDir {
    dirs '../libs'
  }
  mavenCentral()
}

run {
  standardInput = System.in
}

// By default Gradle embeds extremely long CLASSPATH,
// which may fail on Windows because of path length limitation.
// This issue happens on Prettier Apex: https://github.com/dangmai/prettier-plugin-apex/issues/645
// This workaround is from https://gist.github.com/jlmelville/2bfe9277e9e2c0ff79b6
tasks.withType(CreateStartScripts).each { task ->
  task.doLast {
    String text = task.windowsScript.text
    text = text.replaceFirst(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, { "${it[1]}*" })
    task.windowsScript.write text
  }
}

tasks.withType(AbstractArchiveTask) {
  // By default, the result ZIP archive is not reproducible,
  // i.e. running `gradle clean distZip` at different times
  // leads to ZIP files that have different md5 sums.
  // This is a workaround to make it reproducible, from:
  // https://dzone.com/articles/reproducible-builds-in-java
  preserveFileTimestamps = false
  reproducibleFileOrder = true
}

distTar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
distZip {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  archiveFileName = "apex-ast-serializer.zip"
}
distZip.dependsOn generateTypeScript
installDist {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
installDist.dependsOn generateTypeScript
installDist.destinationDir = file("../../prettier-plugin-apex/vendor/apex-ast-serializer")

jar {
  exclude('net/dangmai/serializer/types/**')
}

ext {
  slf4jVersion = '2.0.17'
  jettyVersion = '12.1.6'
  jerseyVersion = '4.0.2'
  junitVersion = '6.0.2'
}

dependencies {
  implementation project(':parser')
  testImplementation(testFixtures(project(":parser")))

  implementation("javax.xml.bind:jaxb-api:2.3.1")
  implementation("org.slf4j:slf4j-api:${slf4jVersion}")
  implementation("org.slf4j:slf4j-simple:${slf4jVersion}")
  implementation("org.eclipse.jetty:jetty-server:${jettyVersion}")
  implementation("org.eclipse.jetty.ee10:jetty-ee10-servlet:${jettyVersion}")
  implementation("org.eclipse.jetty.ee10:jetty-ee10-servlets:${jettyVersion}")
  implementation("org.glassfish.jersey.core:jersey-server:${jerseyVersion}")
  implementation("org.glassfish.jersey.containers:jersey-container-servlet:${jerseyVersion}")
  implementation("org.glassfish.jersey.containers:jersey-container-jetty-http:${jerseyVersion}")
  implementation("org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}")
  implementation("org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}")

  // https://mvnrepository.com/artifact/jakarta.activation/jakarta.activation-api
  implementation("jakarta.activation:jakarta.activation-api:2.1.4")

  // https://mvnrepository.com/artifact/org.glassfish.jaxb/jaxb-runtime
  implementation("org.glassfish.jaxb:jaxb-runtime:4.0.6")

  // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher:6.0.2")

  // https://mvnrepository.com/artifact/cz.habarta.typescript-generator/typescript-generator-core
  compileOnly("cz.habarta.typescript-generator:typescript-generator-core:3.2.1263")
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}
jacocoTestReport {
  reports {
    xml.required = true
  }
  dependsOn test
}

generateTypeScript {
  jsonLibrary = 'jackson2'
  classes = [
    'apex.jorje.semantic.compiler.parser.ParserOutput',
  ]
  classPatterns = [
    'apex.jorje.data.**',
    'apex.jorje.parser.impl.HiddenToken**',
  ]
  excludeClassPatterns = [
    'apex.jorje.**$MatchBlock',
    'apex.jorje.**$MatchBlockWithDefault',
    'apex.jorje.**$SwitchBlock',
    'apex.jorje.**$SwitchBlockWithDefault',
    'apex.jorje.**$Visitor',
    'apex.jorje.**Factory',
    'apex.jorje.**Decorator',
  ]
  customTypeNaming = [
    'apex.jorje.data.ast.Expr$LiteralExpr:ExprLiteralExpr',
    'apex.jorje.data.soql.QueryExpr$LiteralExpr:QueryExprLiteralExpr',
  ]
  customTypeProcessor = 'net.dangmai.serializer.types.CustomTypeProcessor'
  outputFile = 'build/typings/jorje.d.ts'
  outputKind = 'module'
  extensionClasses = [
    'net.dangmai.serializer.types.CustomEnumExtension',
    'net.dangmai.serializer.types.CustomFieldExtension',
    'net.dangmai.serializer.types.UnionTypeExtension',
    'net.dangmai.serializer.types.GenericNodeExtension',
  ]
  sortDeclarations = true
  noFileComment = true
}
