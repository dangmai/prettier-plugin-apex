plugins {
    id 'java'
    id 'application'
}

group 'net.dangmai'
version '1.0-SNAPSHOT'

task httpStartScripts(type: CreateStartScripts) {
    mainClassName = 'net.dangmai.serializer.server.HttpServer'
    applicationName = 'apex-ast-serializer-http'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

applicationDistribution.into("bin") {
    from(httpStartScripts)
    fileMode = 0755
}

mainClassName = 'net.dangmai.serializer.Apex'

sourceCompatibility = 1.8

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven { url "https://jitpack.io" }
}

run {
    standardInput = System.in
}

tasks.withType(AbstractArchiveTask) {
    // By default, the result ZIP archive is not reproducible,
    // i.e. running `gradle clean distZip` at different times
    // leads to ZIP files that have different md5 sums.
    // This is a workaround to make it reproducible, from:
    // https://dzone.com/articles/reproducible-builds-in-java
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

distZip {
    duplicatesStrategy = 'exclude'
    archiveName = "${project.name}.zip"
}
ext {
    slf4jVersion = '1.7.30'
    jettyVersion = '9.4.28.v20200408'
    jerseyVersion = '2.30.1'
}

dependencies {
    implementation name: 'apex-jorje-lsp'
    // https://mvnrepository.com/artifact/com.thoughtworks.xstream/xstream
    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.12'

    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
    implementation "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
    implementation "org.glassfish.jersey.containers:jersey-container-servlet-core:${jerseyVersion}"
    implementation "org.glassfish.jersey.containers:jersey-container-jetty-http:${jerseyVersion}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}"
    implementation "org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}"

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'

    // https://mvnrepository.com/artifact/org.json/json
    testImplementation group: 'org.json', name: 'json', version: '20200518'
}

test {
    useJUnitPlatform()
}
